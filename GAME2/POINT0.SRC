

liml	equ 8
limr	equ 238+4
limu	equ 14+4
limd	equ 214
mpspd	equ 4

	;pointer phase vectors
ppvl	dl pgpxy,mmpxy,cspxy,mppxy,c1pxy,s1pxy,qcpxy,s2pxy
	dl inpxy,in1pxy,in2pxy,in3pxy,ni1pxy,eqppxy,tcpxy
	dl pwpxy
ppvh	dh pgpxy,mmpxy,cspxy,mppxy,c1pxy,s1pxy,qcpxy,s2pxy
	dh inpxy,in1pxy,in2pxy,in3pxy,ni1pxy,eqppxy,tcpxy
	dh pwpxy

	;pprvl	dl pgrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv  
	;pprvh	dh pgrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv,csrv

	;hot spots x1,y1 x2,y2
	;main game
pgpxy	db 16,168,40,192		;next player
	db 48,168,72,192		;combat
	db 80,168,104,192		;unlock
	db 176,168,200,192	;backpack
	db 112,168,136,192	;search doors
	db 144,168,168,192	;search treasure
	db 208,168,232,192	;map
	db 178,138,195,147	;move up/left
	db 197,138,214,147	;move up/right
	db 178,149,195,158	;move down/left
	db 197,149,214,158	;move down/right
	db 16,136,40,158		;cast spell (wiz/elf only)

	db 108,48,124,56		;floor tiles
	db 124,56,140,64
	db 140,64,156,72
	db 156,72,172,80
	db 172,80,188,88
	db 188,88,204,96
	db 204,96,220,104
	db 220,104,236,112	

	db 92,56,108,64
	db 108,64,124,72		;floor tiles
	db 124,72,140,80
	db 140,80,156,88
	db 156,88,172,96
	db 172,96,188,104
	db 188,104,204,112
	db 204,112,220,120

	db 76,64,92,72
	db 92,72,108,80
	db 108,80,124,88
	db 124,88,140,96
	db 140,96,156,104
	db 156,104,172,112
	db 172,112,188,120
	db 188,120,204,128

	db 60,72,76,80
	db 76,80,92,88
	db 92,88,108,96
	db 108,96,124,104
	db 124,104,140,112
	db 140,112,156,120
	db 156,120,172,128
	db 172,128,188,136

	db 44,80,60,88
	db 60,88,76,96
	db 76,96,92,104
	db 92,104,108,112
	db 108,112,124,120
	db 124,120,140,128
	db 140,128,156,136
	db 156,136,172,144

	db 28,88,44,96
	db 44,96,60,104
	db 60,104,76,112
	db 76,112,92,120
	db 92,120,108,128
	db 108,128,124,136
	db 124,136,140,144
	db 140,144,156,152

	db 12,96,28,104
	db 28,104,44,112
	db 44,112,60,120
	db 60,120,76,128
	db 76,128,92,136
	db 92,136,108,144
	db 108,144,124,152
	db 124,152,140,168
	db 0			;end


	;main menu
mmpxy	db 68,90,108,150		;start/select quest
	;db 100,92,138,148		;select character
	db 132,90,172,150		;equip character
	db 0

	;character selection
cspxy	db 22,37,58,90		;barbarian
	db 78,37,114,90		;dwarf
	db 134,37,170,90		;elf
	db 190,37,226,90		;wizard
	;db 61,117,91,163		;in play toggle
	;db 109,117,139,163	;rename
	;db 157,117,187,163	;refresh
	;db 114,178,134,190	;exit
	db 0

	;map
mppxy	db 230,190,242,214	;exit
	db 20,36,228,188		;identify
	db 0

c1pxy	db 0

	;spell select screen (pre game)
s1pxy	db 29,28,67,85		;water
	db 180,28,220,85		;air
	db 29,132,67,188		;fire
	db 180,132,220,188	;earth
	db 0

	;quest select (pre game)
qcpxy	db 8,34,242,188		;quests
	db 8,196,242,202		;more
	db 8,204,242,212		;exit
	db 0

	;spell select (in game)
s2pxy	db 8,84,242,156		;spells
	db 8,164,242,174		;cancel
	db 0

	;inventory	
inpxy	db 229,28,242,45		;potions
	db 229,76,242,92		;weapon
	db 229,122,242,142	;armour
	db 229,162,242,180	;toolkit
	db 229,194,242,210	;exit
	db 0

	;potions (inv)
in1pxy	db 8,84,242,124+8		;potions
	db 8,132+8,242,140+8	;cancel
	db 0

	;weapons (inv)
in2pxy	db 8,84,242,156		;weapons
	db 8,164,242,172		;cancel
	db 0

	;armour (inv)
in3pxy	db 8,84,242,140		;armour
	db 8,148,242,158		;cancel
	db 0

	;name input
ni1pxy	db 108,92+16,132,100+16		;delete
	db 140,92+16,164,100+16		;end
	db 0

	;equip
eqppxy	db 28,22,52,58		;babarian
	db 84,22,108,58		;dwarf
	db 140,22,164,58		;elf
	db 196,22,220,58		;wizard
	db 8,92,242,196		;equipment
	db 8,204,242,214		;exit
	db 0

	;choose players/music
tcpxy	db 8,100,242,138		;1/2 player & music on/off
	db 0

	;password
pwpxy	db 100,100,124,108	;del
	db 132,100,156,108	;end
	db 0

	;hot spot routine vectors
pgrv	dw pgrr1,pgr2,pgr3,pgr4,srch_door,pgr6,pgr7,pgr8,pgr9,pgr10
	dw pgr11,pgr12
csrv	dw csr1,csr2,csr3,csr4,csr5,csr6,csr7,csr8



	;pointer
p_and_c	lda p_chnge
	bne !n2a

	lda dfirea
	beq !n1
	jmp click

!n1	;lda dfireb	;!!!!!!!!!!!
	;beq !n2a	
	;rts
	;inc buildflg
	;lda dnmet
	;eor #1
	;sta dnmet	;!!!!!!!!!!!	
	;rts

!n2a	lda ud
	bne !n2
	lda lr
	bne !n2
	
	lda #0	;stop
	sta inadd
	beq !n4

!2a	lda mudd	
	beq !n3
	bmi !1
	jsr movpydn
	jmp !n3
!1	jsr movpyup
!n3	lda mlrd
	beq !xx
	bmi !2
	jmp movpxrt
!2	jmp movpxlt

!n4	sta mudd
	sta mlrd
!xx	rts

!n2	inc indel
	lda indel
	eor #5
	bne !2b
	sta indel
	lda inadd
	cmp #mpspd
	beq !2b
	inc inadd

!2b	jsr poiud

poilr	lda lr
	sta mlrd
	beq !xx
	bmi !lt
!rt	lda #limr
	cmp obxl+point
	beq !x
	bcs !o1
	bne !x
!o1	jmp movpxrt
!lt	lda #liml
	cmp obxl+point
	bcs !x
!o3	jmp movpxlt
!x	sta obxl+point
	lda #0
	sta mlrd
!xx	rts

poiud	lda ud
	sta mudd
	beq !xx
	bmi !up
!dn	lda #limd
	cmp obyl+point
	beq !x
	bcs !o2
	bne !x
!o2	jmp movpydn
!up	lda #limu
	cmp obyl+point
	bcs !x
!o3	jmp movpyup
!x	sta obyl+point
	lda #0
	sta mudd
!xx	rts

movpydn	lda obyl+point
	clc
	adc inadd
	sta obyl+point
	rts
movpyup	lda obyl+point
	sec
	sbc inadd
	sta obyl+point
	rts
movpxrt	lda obxl+point
	clc
	adc inadd
	sta obxl+point
	rts
movpxlt	lda obxl+point
	sec
	sbc inadd
	sta obxl+point
	rts



click	ldx pphase	;pointer phase
	lda ppvl,x
	sta t2
	lda ppvh,x
	sta t3
	
	ldx #0

!l1	lda #0
	sta t1
	txa
	asl a
	rol t1
	asl a
	rol t1
	sta t0

	lda t0
	clc
	adc t2
	sta t0
	lda t1
	adc t3
	sta t1

	ldy #0
	lda (t0),y
	beq !x
	cmp obxl+point
	beq !ok1
	bcs !nxt
!ok1	iny
	lda (t0),y
	cmp obyl+point
	beq !ok2
	bcs !nxt
!ok2	iny
	lda (t0),y
	cmp obxl+point
	bcc !nxt
!ok3	iny
	lda (t0),y
	cmp obyl+point
	bcs !okay

!nxt	inx
	bne !l1
!x	rts

!okay	lda pphase
	bne !o1
	lda #<pgrv
	ldy #>pgrv
	jmp !o2
!o1	lda #<csrv
	ldy #>csrv
!o2	sta t0
	sty t1
		
	txa
	cmp #$c
	bcs !ohoh
	asl a
	tay
	lda (t0),y
	sta t2
	iny
	lda (t0),y
	sta t3
	sfx 0
	jmp (t2)

!ohoh	ldx tile_click
	bne !x
	sec
	sbc #$c
	sta tile_num
	inc tile_click
	rts	

;* ------------------------------------------------------------------------ *;
pgrr1	jsr pgr1
	ldx player_n
	bmi !y
	lda player_out,x
	bne pgrr1
!y	rts

pgr1	ldx player_n
	bmi !a1
	lda player_flg,x
	and #(missgo+invisible+thrurock)~$ff
	sta player_flg,x
!a1	txa
	beq !p2
	cmp #1
	beq !p3
	cmp #2
	beq compt
	lda #0
!ok	sta player_n
	lda #8
	sta flshcnt
	inc p_chnge
	inc buildflg
!x	rts

!p2	lda two_play
	beq !p3
	lda #1
	bne !ok
!p3	lda quest_num
	cmp #1
	bne compt
	lda sir_ragnar
	beq compt
	lda #2
	bne !ok
compt	lda #$ff
	sta player_n
	inc comp_turn
	rts


;* ------------------------------------------------------------------------ *;
	
	;$@$
	;nme stats
	;nme_mov	hex 08 0a 04 06 06 04 06 06	;max no. squares can move
	;no. of dice attack
nme_atk	hex 03 02 02 03 03 03 04 02  04 02 02 05 04 04	04 04 05 04 04
	; "   "   "  defend
nme_def	hex 02 01 03 04 03 04 04 02	 01 05 04 06 04 03 04 06 06 06 06
plr_atk	hex 03 02 02 01 00
plr_def	hex 02 02 02 02 02
pl_fab	hex 59 5f 65 6b 65
pl_dab	hex 5c 62 68 6e 68
pl_dab1	hex 8a 8b 8c 8d 8c
pl_stx	hex 68 60 60 68 60
nm_fab	hex 80 7a 86 71 74 7d 77 83  80 71 71 71 80 83 77 77 71 71 71
nm_xpos	hex 74 78 74 78 74 74 78 70  74 78 78 78 74 70 78 78 78 78 78
nm_ypos	hex 3c 40 38 28 3c 38 2c 30  3c 28 28 28 3c 30 2c 2c 28 28 28
skull1	equ $16
shld1	equ $1a
frmxpos	hex 68 68 68 65 62 60
	hex 60 68 68 65 62 60
	hex 60 60 6a 65 62 60
	hex 68 68 68 68 68 60
frmsxp	hex 68 60 60 68
nmetyp	hex 1c 1d 1e 1f 20 21 22 23  1c 1f 1f 1f 1c 23 22 22 1f 1f 1f
validop1	equ 2
validop2	equ $15


	;combat
p2xx	rts
pgr2	ldy player_n
!ab	lda icon_oo,y	;combat icon active?
	and #i_combat
	bne p2xx	
	jsr missago
	bne p2xx
	lda quest_num	;got fools gold
	cmp #9
	bne !ow
	lda fools_gold
	bne p2xx

!ow	jsr mapo	;output visible map and players/monsters etc
	print1 txt3a,scrn0+26*32+6,32
	lda #2
	sta phase
	jsr fadeup

!l1	jsr mpbits1	;!!!!
	ldx dfirea
	bne !or1
	jsr map_ident	;choose opponent
	lda mmdex
	sta tmmdex
	jmp !l1

!or1	lda t10
	cmp #1
	bne !w2
	jmp mpxit
!w2	lda tmmdex	;valid opponent?
	sta mmdex
	cmp #validop1
	bcc !l1
	cmp #validop2
	bcs !l1

	jsr spesh_attk
	bcc !l1
	
!n1	jsr zero_pmove	
	lda #i_sdoors+i_streas+i_magic+i_combat
	sta icon_oo,x
	
	jsr fadedown
	jsr getpxy	;combat setups
	
	jsr spesh_weap
	bcs !yup
	jmp mpxit

!yup	ldx #1
fyup	stx fight_flg
	dex
	stx obstat+point
	stx ob_attk
!fyup	lda player_n
	sta pnum
	jsr setcombat	;set combat screen up
	jsr setpfig
	ldx plr_t
	lda pl_fab,x
	sta obfrm+obuse
	jsr setxxp
	ldx nme_t
	lda nmetyp,x
	jsr setmfig
	clc
	adc #2
	sta obfrm+obuse-1
	
	ldx nme_t	;get nme dice rolls
	ldy nme_def,x
	cpx #3	;chaos warrior?
	bne !n9	
	lda quest_num	;stone hunter?
	cmp #6
	bne !n9
	iny		;yes: inc defence by 1
!n9	ldx #0
	stx t2
!l1	jsr randy
	and #3
	beq !o1
	lda #1
	sta scratch+$14,x
	inc t2
	inx
!o1	dey
	bne !l1
	lda #$ff
	sta scratch+$14,x

	ldx plr_t	;get basic characters dice rolls
	lda plr_atk,x
	sta t0
	jsr attacks
	ldy t0
	ldx #0
	stx t3
	cpy #0
	beq !fn1
!l2	jsr randy
           and #1
	beq !o2
	sta scratch+$1c,x
	inc t3
	inx
!o2	dey
	bne !l2
!fn1	lda #$ff
	sta scratch+$1c,x

	lda t3
	beq !oo1
	ldy #skull1
	jsr ptleft
!oo1	lda t2
	beq !oo2
	ldy #shld1
	jsr ptrite

!oo2	jsr hobs
	jsr fadeup

	ldx #1
	jsr delay

	ldx #0	;combat control
!fl1	stx tmpx
	lda #0
	sta scratch+$10
	sta scratch+$11
	sta scratch+$12
	lda #9
	sta scratch+$13
	
	lda scratch+$1c,x
	bpl !ok1
	jmp !fight_fin
!ok1	cmp scratch+$14,x
	beq !adf

	sfx 14
	ldx nme_n
	jsr nme_off	;get rid of nme
	inc scratch+$10
	
!atk_arh	jsr waitfrm	;character attacks and monster gets hurt
	jsr hobs
	inc scratch+$12
	lda scratch+$12
	and #3
	bne !atk_arh

	lda scratch+$11
	cmp #2
	bne !o3
	lda scratch+$13
	beq !ff
	dec scratch+$13
	lda obstat+obuse-1
	eor #obon
	sta obstat+obuse-1
	jmp !atk_arh
!ff	lda #0
	sta obstat+obuse-1
	ldx #1
	jsr delay
	jmp !fight_fin
	
!o3	inc obfrm+obuse
	jsr setxxp
	
	inc scratch+$11
	lda scratch+$11
	cmp #2
	bne !atk_arh
	dec obfrm+obuse-1
	sfx 18
	bne !atk_arh

!adf	sfx 14
	
!atk_def	jsr waitfrm	;character attacks and monster defends
	jsr hobs
	inc scratch+$12
	lda scratch+$12
	and #3
	bne !atk_def

	lda scratch+$11
	cmp #2
	beq !fini
	inc obfrm+obuse
	dec obfrm+obuse-1
	jsr setxxp
	inc scratch+$11
	bne !atk_def
!fini	ldx plr_t	;reset anim
	lda pl_fab,x
	sta obfrm+obuse
	jsr setxxp
	ldx nme_t
	lda nm_fab,x
	clc
	adc #2
	sta obfrm+obuse-1
	ldx #$70
	jsr delay1
	jsr waitfrm
	jsr hobs
	
!loopit	ldx #1
	jsr delay

	ldx tmpx
	inx
	jmp !fl1

!fight_fin	jsr gen_txt
	lda scratch+$10
	sta nme_a_d
	beq !survive
	
	jsr kill_spesh
	jmp !prn

!survive	lda #<txt5d	;monster survived
	ldx #>txt5d

!prn	jsr prntff
	jsr fadeup
	
	jsr waitfire

	lda nme_a_d
	bne !nok
	ldx plr_t
	lda player_pou,x	;2 attacks if hero brew taken
	and #herobrew
	beq !nok1
	bne !yya
!nok1	lda player_wpu,x	;2 attacks if weapon = 
	and #weapon
	cmp #9	;orcs bane
	bne !nok
	lda nme_t
	bne !nok
!yya	lda ob_attk
	bne !nok
	inc ob_attk
	jsr fadedown
	jmp !fyup	;another go
		
!nok	ldx #0
	stx fight_flg
	inx
	stx obstat+point
	
!xit	jmp mpxit


spesh_weap	lda #5	;staff
	sta t10
	lda player_t,x
	cmp #3
	beq !fd	;wizard
	tax		;certain weapons allow you to fight differently
	lda player_wpu,x
	and #weapon
	sta t10
	cmp #2	;crossbow
	beq !fd
	cmp #4	;spear/staff/shortsword
	bcc !1
	cmp #7
	bcs !1

!fd	dec t1e	;wizard can fight diagonally
	dec t1f
	jsr cmpnmexy
	bcs !yup
	inc t1e
	inc t1e
	jsr cmpnmexy
	bcs !yup
	inc t1f
	inc t1f
	jsr cmpnmexy
	bcs !yup
	dec t1e
	dec t1e
	jsr cmpnmexy
	bcs !yup
	inc t1e
	dec t1f
!1	dec t1f
	jsr cmpnmexy
	bcs !yup
	inc t1e
	inc t1f
	jsr cmpnmexy
	bcs !yup
	dec t1e
	inc t1f
	jsr cmpnmexy
	bcs !yup
	dec t1e
	dec t1f
	jsr cmpnmexy
	bcs !yup

	jsr getpxy
	
	lda t10
	cmp #2
	beq !crossbow
	cmp #4
	beq !spear
	cmp #7
	bne !ohoh
	beq !handaxe
!yup	jmp !yup1

!handaxe	jsr line_o
	ldx traj_nme
	bmi !noline
	cpx traj_targ
	bne !noline
	jsr nme_off	;nme dead
	lda #handaxe~$ff	;lose handaxe
	jsr lose_it
	jsr readyt	;nme killed 
	jsr box2
	lda #<txt104
	ldx #>txt104
	jmp !ab

!spear	jsr line_o
	ldx traj_nme
	bmi !noline
	cpx traj_targ
	bne !noline
	jsr nme_off	;nme dead
	lda #spear~$ff	;lose spear
	jsr lose_it
	jsr readyt	;nme killed 
	jsr box2
	lda #<txt103
	ldx #>txt103
	jmp !ab

!crossbow	jsr line_o
	ldx traj_nme
	bmi !noline
	cpx traj_targ
	bne !noline
	jsr nme_off
	ldx plr_t	;lose potion of strength
	lda player_pou,x
	and #potstren~$ff
	sta player_pou,x
	jsr readyt	;nme killed 
	jsr box2
	lda #<txt105
	ldx #>txt105
	jmp !ab

!noline	jsr readyt	;obscured target
	jsr box2
	lda #<txt11a
	ldx #>txt11a
	jmp !aab

!ohoh	jsr readyt	;not in range
	jsr box2	
	lda #<txt3e
	ldx #>txt3e
	jmp !aab

!ab	jsr prntff
	jsr fadeup
	jsr waitfire
	jsr kill_spesh
	sta t6
	stx t7
	cmp #<txt5c	;normal message
	bne !yy
	cpx #>txt5c
	beq !nop
!yy	jsr fadedown
	jsr box2
	lda t6
	ldx t7
!aab	jsr prntff
	jsr fadeup
	jsr waitfire
!nop	clc
	rts

!yup1	lda t10
	cmp #2	;crossbow
	bne !norm
	jsr readyt	;crossbow to close
	jsr box2	
	lda #<txt102
	ldx #>txt102
	jmp !aab
!norm	sec
	rts

lose_it	sta t11
	ldx player_n
	ldy player_t,x
	lda player_wpg,y
	and t11
	sta player_wpg,y
	lda #0	;using no weapon
	sta player_wpu,y
	rts

spesh_attk	lda quest_num	;can't hurt gargoyle until trapped
	cmp #$a	;chest set off
	bne !n1	;in bastion of chaos
	lda mmdex
	cmp #8
	bne !yes
	lda garg_hurt
	beq !no
	bne !yes
!n1	cmp #$b	;can't hurt witch lord
	bne !n2	;on barak tor
	lda mmdex
	cmp #$c
	bne !yes
	jsr fadedown
	jsr amess1
	lda #<txtd2
	ldx #>txtd2
!zob	jsr prntff
	jsr fadeup
	jsr waitfire
	pla
	pla
	jmp mpxit
!n2	cmp #$d	;only attack witch lord with
	beq !y1	;spirit blade
	cmp #$17
	bne !n3
!y1	lda mmdex
	cmp #$d
	bne !yes
	ldx plr_t
	lda player_wpu,x	;weapon changes attack dice
	and #weapon
	cmp #8	;spirit blade
	beq !yes
	jsr fadedown
	jsr amess1
	lda #<txtd4
	ldx #>txtd4
	jmp !zob		

!n3	sec
	rts
!no	clc
	rts
!yes	sec
	rts


kill_spesh	lda nme_t	;was nme ulag?
	cmp #ulag
	bne !1
	lda plr_t	;set player who killed ulag
	sta plr_killer	
	inc ulag_dead	;ulag is dead
	lda #<txt85
	ldx #>txt85
	rts

!1	lda quest_num
	cmp #$a
	bne !2
	lda nme_t
	beq !o
	cmp #1
	beq !g
	cmp #3
	beq !cf
	cmp #4
	beq !cf
	jmp !norm
!o	lda #20	;20 gc
	jsr srgg
	lda #<txtce	;bounty
	ldx #>txtce
	rts
!g	lda #10
	jsr srgg
	lda #<txtcd
	ldx #>txtcd
	rts
!cf	lda #30
	jsr srgg
	lda #<txtcf
	ldx #>txtcf
	rts

!2	cmp #$b
	bne !3
	lda room_num
	cmp #$6
	beq !o1
	jmp !norm
!o1	lda #200	;200 gc
	jsr srgg	;star of west
	lda #<txtd0
	ldx #>txtd0
	rts

!3	cmp #$d
	bne !4
	lda room_num
	cmp #$7
	beq !o2
	jmp !norm
!o2	inc completed	;game is complete
	lda #<txtd5	;witch lord is dead
	ldx #>txtd5
	rts

!4	cmp #7
	bne !5
	lda room_num
	cmp #$16
	bne !norm
	inc balur_dead
	lda #<txtdc
	ldx #>txtdc
	rts

!5	cmp #0
	bne !6
	lda nme_t
	cmp #$e
	bne !norm
	inc verag_dead
	lda #<txte8
	ldx #>txte8
	rts

!6	cmp #$12
	bne !7
	lda nme_t
	cmp #$f
	bne !norm
	inc blthr_dead
	lda #<txtea
	ldx #>txtea
	rts

!7	cmp #$13
	bne !8
	lda room_num
	cmp #$20
	bne !norm
	jsr tc7	;give orcs bane
	lda #<txtcc
	ldx #>txtcc
	rts

!8	cmp #$14
	bne !9
	lda nme_t	;skulmar?
	cmp #$10	
	bne !norm
	lda #<txteb
	ldx #>txteb
	rts	

!9	cmp #$16	;kessandria
	bne !a
	lda nme_t
	cmp #$11
	bne !norm
	lda #<txtee
	ldx #>txtee
	rts	

!a	cmp #$17
	bne !norm
	lda nme_t
	cmp #$b			
	bne !norm
	inc finished
	lda #<txtf0
	ldx #>txtf0
	rts


!norm	lda #<txt5c	;normal message
	ldx #>txt5c
	rts



setpfig	lda #a_player	;players sprite
	sta obtyp+obuse	
	lda #$40
	sta obyl+obuse
	lda #obon
	sta obstat+obuse
	rts

setmfig	sta obtyp+obuse-1	
	lda nm_xpos,x
	sta obxl+obuse-1
	lda nm_ypos,x
	sta obyl+obuse-1
	lda #obon
	sta obstat+obuse-1
	lda nm_fab,x
	rts


setxxp	lda obfrm+obuse
	cmp #$8a
	bcc !ok
	sec
	sbc #$8a
	tay
	lda frmsxp,y
	bne !o1
!ok	sec
	sbc #$59
	tay
	lda frmxpos,y
!o1	sta obxl+obuse
	rts


ptleft	sta td
	sty tc
	lda #>scrn0+6*32+8
	sta tb
	sta tf
	sta $2006
	lda #<scrn0+6*32+8
	sta ta
	sta te
	sta $2006
ptloop	ldx #0
!l1	ldy tc
	sty $2007
	iny
	sty $2007
	jsr ta_add_32
	iny
	sty $2007
	iny
	sty $2007
	jsr ta_add_32
	inx
	cpx #5
	bne !1
	lda te
	clc
	adc #2
	sta ta
	pha
	lda tf
	adc #0
	sta tb
	sta $2006
	pla
	sta $2006
!1	cpx td
	bne !l1
	rts
ptrite	sta td
	sty tc
	lda #>scrn0+6*32+21
	sta tb
	sta tf
	sta $2006
	lda #<scrn0+6*32+21
	sta ta
	sta te
	sta $2006
	jmp ptloop

ta_add_32	lda ta
	clc
	adc #$20
	sta ta
	pha
	lda tb
	adc #0
	sta tb
	sta $2006
	pla
	sta $2006
	rts


weap_atk	hex 00 04 03 03 02 02 02 02 03 02
attacks	lda player_wpu,x	;weapon changes attack dice
	sta t1
	and #weapon
	tay
	beq !no1	;not got a weapon
	lda weap_atk,y
	sta t0

!no1	cpy #8	;spirit blade
	bne !no2
	lda nme_t	;fighting ske/zom/mum
	cmp #2
	beq !a1
	cmp #5
	beq !a1
	cmp #7
	bne !no2
!a1	inc t0	;4 if ske/mum/zom

!no2	ldy pnum
	lda player_flg,y
	and #courage	;courage spell cast?
	beq !no4
	inc t0	;+2 attack dice
	inc t0
	
!no4	lda player_pou,x	;strength potion taken
	and #potstren
	beq !no5
	lda player_pou,x	;lose strength potion
	and #potstren~$ff
	sta player_pou,x
	inc t0	;+2 attack dice
	inc t0
!no5	jmp fools


cmpnmexy	lda t1e
	cmp nme_x
	bne !n
	lda t1f
	cmp nme_y
	bne !n
	sec
	rts
!n	clc
	rts


; ------------------------------------------------------

killer	ldx #0	
	stx fight_flg
	jsr setcombat	;set combat screen up
	jsr setpfig
	ldx plr_t
	lda pl_dab1,x
	sta obfrm+obuse
	lda pl_stx,x
	sta obxl+obuse
	ldx nme_t
	lda nmetyp,x
	jsr setmfig
	sta obfrm+obuse-1
	
	ldx nme_t	;get nme dice rolls
	ldy nme_atk,x
	ldx #0
	stx t2
!l1	jsr randy
	and #1
	beq !o1
	sta scratch+$14,x
	inc t2
	inx
!o1	dey
	bne !l1
	lda #$ff
	sta scratch+$14,x

	lda #2	;plr_def,x
	sta t0
	ldx plr_t	;get basic characters dice rolls
	cpx #4
	beq !o1a
	jsr defends
!o1a	ldy t0
	ldx #0
	stx t3
	cpy #0
	beq !fn1
!l2	jsr randy
           and #1
	beq !o2
	sta scratch+$1c,x
	inc t3
	inx
!o2	dey
	bne !l2
!fn1	lda #$ff
	sta scratch+$1c,x

	lda t3
	beq !oo1
	ldy #shld1
	jsr ptleft
!oo1	lda t2
	beq !oo2
	ldy #skull1
	jsr ptrite

!oo2	jsr hobs
	jsr pbody
	jsr fadeup

	ldx #1
	jsr delay

	ldx plr_t
	lda pl_dab,x
	clc
	adc #2
	sta scratch+$f

	ldx #0	;combat control
!fl1	stx tmpx
	lda #0
	sta scratch+$10
	sta scratch+$11
	sta scratch+$12
	lda #9
	sta scratch+$13
	
	lda scratch+$14,x
	bpl !ok1
	jmp !fight_fin
!ok1	cmp scratch+$1c,x
	bne !oo1a
	jmp !atd2	;atk_def

!oo1a	ldx plr_t
	lda player_pou,x	;lose hero brew potion
	and #herobrew~$ff
	sta player_pou,x
	dec player_bp,x
	ldx pnum
	lda player_flg,x
	and #rockskin~$ff
	sta player_flg,x
	jsr pbody
	sfx 1c
			
!atk_arh	jsr waitfrm	;monster attacks and character gets hurt
	jsr hobs
	inc scratch+$12
	lda scratch+$12
	and #3
	bne !atk_arh

	ldx plr_t	;has player died
	lda player_bp,x
	bne !q2
	lda obfrm+obuse
	cmp pl_dab1,x
	bne !w1
	sfx 20
	lda pl_dab,x
	sta obfrm+obuse
	jsr setxxp
	bne !atk_arh
!w1	inc obfrm+obuse	;animate death
	inc obfrm+obuse-1
	jsr setxxp
	lda obfrm+obuse
	cmp scratch+$f
	bne !atk_arh
	jsr waitfrm
	jsr hobs
	ldx #1
	jsr delay
	inc scratch+$10
	jmp !fight_fin	;fight finished

!q2	lda scratch+$11	
	cmp #2
	beq !fini
	cmp #2
	bcs !q1
	lda obfrm+obuse
	cmp pl_dab1,x
	bne !w4
	lda pl_dab,x
	sta obfrm+obuse
	bne !q1
!w4	inc obfrm+obuse
!q1	jsr setxxp
	inc obfrm+obuse-1
	inc scratch+$11
	bne !atk_arh

!atd2	sfx 14
!atk_def	jsr waitfrm	;monster attacks and character defends
	jsr hobs
	inc scratch+$12
	lda scratch+$12
	and #3
	bne !atk_def

	lda scratch+$11
	cmp #2
	beq !fini
	cmp #1
	bcs !q3
	ldx plr_t
	lda obfrm+obuse
	cmp pl_dab1,x
	bne !w2
	lda pl_dab,x
	sta obfrm+obuse
	bne !q3
!w2	inc obfrm+obuse
!q3	jsr setxxp
	inc obfrm+obuse-1
	inc scratch+$11
	bne !atk_def

!fini	ldx plr_t	;reset anim
	lda pl_dab1,x
	sta obfrm+obuse
	lda pl_stx,x
	sta obxl+obuse
	ldx nme_t
	lda nm_fab,x
	sta obfrm+obuse-1
	ldx #$70
	jsr delay1
	jsr waitfrm
	jsr hobs
	
!loopit	ldx #1
	jsr delay

	ldx tmpx
	inx
	jmp !fl1

!fight_fin	jsr gen_txt
	lda scratch+$10
	beq !survive
	jmp plr_dead	;set player dead flg
!survive	lda #<txt5e	;player survived
	ldx #>txt5e
!prn	jsr prntff
	jsr fadeup
	
	jsr waitfire

!xit	rts


defends	lda player_amu,x	;plater armour?
	sta t1
	and #platearm
	beq !n1
	lda #4	;4 def
	bne !n3
!n1	lda t1
	and #chainarm	;chain mail?
	beq !n2
	lda #3	;3 def
	bne !n3
!n2	lda t1
	and #borinsa	;borins armour?
	beq !n4
	lda #4	;4 def
!n3	sta t0
!n4	lda t1
	and #cloakpro	;cloak of protection?
	beq !n5
	inc t0	;+1 def
!n5	lda t1
	and #helmet	;helmet?
	beq !n5a
	inc t0	;+1 def
!n5a	lda t1
	and #shield	;shield?
	beq !n5b
	inc t0	;+1 def
!n5b	lda t1
	and #bracers	;bracers?
	beq !n5c
	inc t0	;+1 def
!n5c	ldy pnum
	lda player_flg,y
	and #rockskin	;rockskin spell cast?
	beq !n6
	inc t0	;+2 def
	inc t0
!n6	lda player_pou,x	;potion of resilience taken?
	and #potresil
	beq fools
	lda player_pou,x	;lose potion
	and #potresil~$ff
	sta player_pou,x
	inc t0	;+2 def
	inc t0

fools	lda quest_num
	cmp #9
	bne !n8
	lda fools_gold
	beq !n8
	lda #0
	sta t0
!n8	rts


